import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "net.sf.aria2"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply plugin: 'me.tatarka.retrolambda'

dependencies {
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:1.8.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'org.jraf:android-switch-backport:1.4.0'
}

tasks.addRule("NDK tasks") { String taskName ->
    Pattern p = Pattern.compile("ndkBuildAria2Abi_(.+)");
    Matcher m = p.matcher(taskName);
    String toolchainDir = "$projectDir.absolutePath/build/toolchain"
    String libsDir = "$projectDir.absolutePath/libs"

    if (m.matches()) {
        String abi = m.group(1)

        def prepTask = taskName.replace("BuildAria2", "Prepare")
        prepTask = task(type: Exec, prepTask) {
            String gccVer = "4.9"
            String toolchain;
            switch (abi) {
                case "x86":
                    toolchain = "x86-${gccVer}"
                    break;
                case "armeabi":
                    toolchain = "arm-linux-androideabi-${gccVer}"
                    break;
                default:
                    throw new UnsupportedOperationException()
            }

            commandLine "$System.env.ANDROID_NDK/build/tools/make-standalone-toolchain.sh", "--platform=android-15",
                    "--toolchain=$toolchain", "--install-dir=$toolchainDir/$abi"

            inputs.file "$System.env.ANDROID_NDK/build/tools/make-standalone-toolchain.sh"
            outputs.dir "$toolchainDir/$abi"
        }

        task(type: Exec, taskName) {
            commandLine "$projectDir.absolutePath/build-natives.sh", "$abi"

            inputs.file "$projectDir.absolutePath/build-natives.sh"
            outputs.dir "$libsDir/$abi"

            dependsOn prepTask
        }
    }
}

task renameExecutables(type: Copy) {
    from "$projectDir.absolutePath/libs"
    into "$projectDir.absolutePath/libs"
    include '**/*'
    exclude '**/*.so'
    exclude '**/*.jar'
    rename(/(.+)/, 'lib$1_exec.so')
}

tasks.renameExecutables.dependsOn ndkBuildAria2Abi_x86, ndkBuildAria2Abi_armeabi

tasks.clean.dependsOn cleanRenameExecutables

tasks.withType(JavaCompile.class).each { JavaCompile compileTask ->
    compileTask.dependsOn renameExecutables
}
